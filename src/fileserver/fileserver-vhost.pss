#!/usr/bin/env pscript
/**
 * Copyright (C) 2017, Hao Hou
 */

/* The Virtual Hosting Static File Server Example */
import("service");
import("config");

var create_service = function(site_list)
{
	var num_sites = len(site_list);

	if(site_list["__default__"] == undefined) 
	{
		print("Site list must have a default site");
		return undefined;
	}

	var selector_init_arg = "dataflow/demux";

	for(var site in site_list)
	{
		if(site != "__default__")
			selector_init_arg += " " + site;
	}

	var fileserver = {
		parsereq    := "getpath";
		parsepath   := "filesystem/parsepath --ext-name";
		guessmime   := "filesystem/mimetype " + mime_type_file;
		selector    := selector_init_arg; 
		merger      := "dataflow/firstnonempty " + num_sites;
		error       := "dataflow/firstnonempty " + (5 + num_sites);
		genres      := "response";

		/* Parse the request */
		() -> "request" parsereq {
			"path" -> "origin" parsepath {
				/* This is the dataflow for the mime type manipulation */
				"extname" -> "extname" guessmime "mimetype" -> "mime" genres;
				/* Here we need to feed the demux */
				"relative" -> "data" selector;
			};
			"host" -> "cond" selector;
		};
		
		merger "out" -> "file" genres "output" -> ();

		/* Handle the internal error */
		{
			parsereq   "__error__" -> "in0";
			parsepath  "__error__" -> "in1";
			guessmime  "__error__" -> "in2";
			selector   "__error__" -> "in3";
			merger     "__error__" -> "in4";
		} error "out" -> "500" genres;

		/* Handle the 400 error */
		parsereq "error" -> "400" genres;

		/* Handle the 403 error */
		parsepath "__null__" -> "403" genres;
	};

	var id = 0;
	var outid = 0;
	for(var site in site_list)
	{
		var name = "readfile_" + id;
		var selector_port = "out" + outid;

		if(site == "__default__") selector_port = "default";
		else outid ++;

		fileserver[name] = "filesystem/readfile " + site_list[site];

		fileserver["@selector"][selector_port] = name + "@path";
		fileserver["@" + name] = {
			result   : "merger@in" + id,
			__error__: "error@in" + (5 + id)
		};

		id++;
	}

	return fileserver;
}

server = create_service({
	"localhost:8080": server_base_dir,
	__default__     : server_base_dir_2
});

/* dump the visualization graph */
Service.visualize(server);

/* then start the file server */
Service.start(server);
