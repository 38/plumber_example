#!/usr/bin/env pscript
/**
 * Copyright (C) 2017, Hao Hou
 **/
/* The main script for the static file server */

// Load the configuration
include "config.pss"

//define the file server
fileserver = {
	parsereq  := "getpath" @[color = green];
	parsepath := "filesystem/parsepath --ext-name";
	guessmime := "filesystem/mimetype " + mime_type_file;
	readfile  := "filesystem/readfile " + server_base_dir;
	genres    := " response" @[color = green];
	error     := "dataflow/firstnonempty 4";

	/* Setup normal logic */
	() -> "request" parsereq "path" -> "origin" parsepath {
		"extname" ->  "extname" guessmime "mimetype" -> "mime";
		"relative" -> "path" readfile "result" -> "file"
	} genres "output" -> ();

	/* What if we got an invalid request */
	parsereq "error" -> "400" genres;

	/* What if we got an invalid path */
	parsepath "__null__" -> "403" genres;

	/* Let's handle the unexpected service error */
	{
		parsereq  "__error__" -> "in0";
		parsepath "__error__" -> "in1";
		guessmime "__error__" -> "in2";
		readfile  "__error__" -> "in3";
	} error "out" -> "500" genres;
};

/* (Optional) dump the visualization graph */
visualize fileserver >>> "/dev/stdout";

/* start the file server */
start fileserver
