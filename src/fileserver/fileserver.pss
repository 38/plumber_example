#!/home/haohou/bin/pscript

include "config.pss"

//define the file server
fileserver = {
	parsereq  := "getpath" @[color = green];
	parsepath := "filesystem/parsepath --ext-name";
	guessmime := "filesystem/mimetype " + mime_type_file;
	readfile :=  "filesystem/readfile " + server_base_dir;
	error     := "dataflow/firstnonempty 4";
	genres   := " response" @[color = green];
	() -> "request" parsereq "path" -> "origin" parsepath {
		"extname" ->  "extname" guessmime "mimetype" -> "mime";
		"relative" -> "path" readfile "result" -> "file"
	} genres "output" -> ();

	parsereq "error" -> "400" genres;
	parsepath "__null__" -> "403" genres;

	{
		parsereq  "__error__" -> "in0";
		parsepath "__error__" -> "in1";
		guessmime "__error__" -> "in2";
		readfile  "__error__" -> "in3";
	} error "out" -> "500" genres;
};

/* dump the visualization graph */
visualize fileserver >>> "/dev/stdout";

/* then start the file server */
start fileserver
