#!/home/haohou/bin/pscript

include "config.pss"

//define the file server
fileserver = {
	parsereq  := "getpath" @[color = green];
	parsepath := "filesystem/parsepath --ext-name";
	guessmime := "filesystem/mimetype " + mime_type_file;
	readfile := "read " + server_base_dir;
	error     := "dataflow/firstnonempty 2";
	genres   := "response" @[color = green];
	
	path2str  := "typing/conversion/raw2str";
	mkrawpath := "typing/conversion/str2raw";
	mkrawmime := "typing/conversion/str2raw";

	() -> "request" parsereq "path" -> "input" path2str "output" -> "origin" parsepath {
		"extname" ->  "extname" guessmime "mimetype" -> "input" mkrawmime "output" -> "mime";
		"relative" -> "input"   mkrawpath "output" -> "path" readfile {
			"content" -> "content";
			"size"    -> "size";
			"status"  -> "status";
		}
	} genres "response" -> ();

	{
		parsereq "error" -> "in0";
		readfile "error" -> "in1";
	} error "out" -> "error" genres;
};

/* dump the visualization graph */
visualize fileserver >>> "/dev/stdout";

/* then start the file server */
start fileserver
